// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl
/**
 * A 16-bit counter with reset, load, and inc control bits.
 * if      (reset(t) == 1) out(t+1) = 0
 * else if (load(t) == 1)  out(t+1) = in(t)
 * else if (inc(t) == 1)   out(t+1) = out(t) + 1
 * else                    out(t+1) = out(t)
 */

CHIP PC {
    IN in[16], load, inc, reset;
    OUT out[16];

    PARTS:
    Mux16 (a=regout, b=incregout, sel=inc, out=incmuxout);
    Mux16 (a=incmuxout, b=in, sel=load, out=loadmuxout);
    Mux16 (a=loadmuxout, b=zeroregout, sel=reset, out=resetmuxout);
    Inc16 (in=regout, out=incregout);
    And16 (a=regout, b=false, out=zeroregout);
    Or (a=load, b=inc, out=loadOrinc);
    Or (a=loadOrinc, b=reset, out=loadOrincOrreset);
    Register (in=resetmuxout, load=loadOrincOrreset, out=regout, out=out);
}
